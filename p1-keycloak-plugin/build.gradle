plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'io.freefair.lombok' version '8.4'       // Lombok
    id 'com.github.johnrengelman.shadow' version '7.1.2'  // Uber-jar
}

group = "${rootProjectgroupId}"
version = "${rootProjectVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'org.junit.platform:junit-platform-engine:1.12.0'
        force 'org.junit.platform:junit-platform-launcher:1.12.0'
        force 'org.junit.platform:junit-platform-commons:1.12.0'

        // Security fixes for vulnerable dependencies
        // Fix HIGH severity CVEs
        force 'commons-beanutils:commons-beanutils:1.11.0'
        force 'org.apache.commons:commons-lang3:3.18.0'

        // Fix gRPC vulnerabilities
        force 'io.grpc:grpc-netty:1.68.0'
        force 'io.grpc:grpc-protobuf:1.68.0'
        force 'io.grpc:grpc-stub:1.68.0'
        force 'io.grpc:grpc-core:1.68.0'

        // Fix Protocol Buffers vulnerability
        force 'com.google.protobuf:protobuf-java:3.25.5'

        // Fix ALL Netty vulnerabilities with latest version (Sept 3, 2025)
        // Fixes: CVE-2025-55163, CVE-2025-24970, CVE-2025-58057, CVE-2025-58056
        force 'io.netty:netty-buffer:4.1.125.Final'
        force 'io.netty:netty-codec:4.1.125.Final'
        force 'io.netty:netty-codec-http:4.1.125.Final'
        force 'io.netty:netty-codec-http2:4.1.125.Final'
        force 'io.netty:netty-codec-socks:4.1.125.Final'
        force 'io.netty:netty-codec-dns:4.1.125.Final'
        force 'io.netty:netty-codec-haproxy:4.1.125.Final'
        force 'io.netty:netty-common:4.1.125.Final'
        force 'io.netty:netty-handler:4.1.125.Final'
        force 'io.netty:netty-handler-proxy:4.1.125.Final'
        force 'io.netty:netty-resolver:4.1.125.Final'
        force 'io.netty:netty-resolver-dns:4.1.125.Final'
        force 'io.netty:netty-transport:4.1.125.Final'
        force 'io.netty:netty-transport-native-unix-common:4.1.125.Final'
        force 'io.netty:netty-transport-native-epoll:4.1.125.Final'
        force 'io.netty:netty-transport-native-kqueue:4.1.125.Final'
    }
}

ext {
    keycloakVersion = '26.3.4'
}

dependencies {
    //–– Keycloak SPIs (compile only, provided by the server) ––
    compileOnly "org.keycloak:keycloak-server-spi:${keycloakVersion}"
    compileOnly "org.keycloak:keycloak-server-spi-private:${keycloakVersion}"
    compileOnly "org.keycloak:keycloak-services:${keycloakVersion}"
    compileOnly "org.keycloak:keycloak-crypto-default:${keycloakVersion}"

    //–– Keycloak test support ––
    testImplementation "org.keycloak:keycloak-junit5:${keycloakVersion}"
    testImplementation "org.keycloak:keycloak-server-spi:${keycloakVersion}"
    testImplementation "org.keycloak:keycloak-server-spi-private:${keycloakVersion}"
    testImplementation "org.keycloak:keycloak-services:${keycloakVersion}"
    testImplementation "org.keycloak:keycloak-crypto-default:${keycloakVersion}"

    //–– Other test frameworks ––
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.0'
    testImplementation 'org.junit.platform:junit-platform-commons:1.12.0'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
    testImplementation 'org.jboss.resteasy:resteasy-core:6.2.4.Final'
    testRuntimeOnly 'net.bytebuddy:byte-buddy-agent:1.15.4'

    //–– Runtime dependencies ––
    implementation 'org.yaml:snakeyaml:2.2'
    // Updated Bouncy Castle to fix CVE-2023-33202
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78'
    // Explicit dependencies to fix CVEs
    implementation 'commons-beanutils:commons-beanutils:1.11.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'com.google.auto.service:auto-service:1.1.1'
    implementation 'org.jboss.logmanager:log4j2-jboss-logmanager:1.1.1.Final'
    implementation 'org.json:json:20231013'
    implementation 'com.slack.api:bolt:1.36.1'
    implementation 'org.freemarker:freemarker:2.3.32'
    implementation 'commons-io:commons-io:2.16.1'

    //–– Quarkus routing extension ––
    //implementation project(':runtime')
    //implementation project(':deployment')
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    archiveClassifier = null
}

test {
    useJUnitPlatform()
    maxParallelForks = project.hasProperty("test.maxParallelForks")
        ? project.property("test.maxParallelForks").toInteger()
        : 2

    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

    // JVM Args to open modules for reflection/testing
    jvmArgs += [
        '--add-opens', 'java.base/java.io=ALL-UNNAMED',
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.util=ALL-UNNAMED',
        '--add-opens', 'java.base/java.net=ALL-UNNAMED',
        '--add-opens', 'java.base/sun.security.x509=ALL-UNNAMED'
    ]

    // Byte Buddy agent
    jvmArgs += [
        "-javaagent:${configurations.testRuntimeClasspath.find { it.name.startsWith('byte-buddy-agent') }}"
    ]
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = file("${buildDir}/jacoco")
}

jacocoTestReport {
    reports {
        html.required = true
        html.destination file("${buildDir}/jacoco/html/")
        xml.required  = true
        xml.destination file("${buildDir}/jacoco/test.xml")
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
