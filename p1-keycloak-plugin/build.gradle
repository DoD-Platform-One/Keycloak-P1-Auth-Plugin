plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    // https://plugins.gradle.org/plugin/io.freefair.lombok
    id 'io.freefair.lombok' version '6.5.0.3'
    // shadowJar plugin is required to create an uberJar with all runtime dependencies
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = "${rootProjectgroupId}"
version = "${rootProjectVersion}"
sourceCompatibility = '11'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.keycloak:keycloak-services:21.0.2'
    testImplementation 'org.keycloak:keycloak-server-spi:21.0.2'
    testImplementation 'org.keycloak:keycloak-server-spi-private:21.0.2'
    testImplementation 'org.keycloak:keycloak-model-legacy:21.0.2'
    testImplementation 'org.keycloak:keycloak-crypto-default:21.0.2'

    compileOnly 'org.keycloak:keycloak-services:21.0.2'
    compileOnly 'org.keycloak:keycloak-server-spi:21.0.2'
    compileOnly 'org.keycloak:keycloak-server-spi-private:21.0.2'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'org.jboss.logmanager:log4j2-jboss-logmanager:1.1.1.Final'
}

jar {
    // disable the default jar task so that shadowJar is used instead
    enabled = false
    dependsOn(shadowJar)
}

// shadowJar inherits from the jar task
// https://imperceptiblethoughts.com/shadow/configuration/
shadowJar {
    // don't append anything to the end of the jar file name
    classifier = null
}

test {
    // avoid error during unit tests for jboss logmanager
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("${buildDir}/jacoco")
}

jacocoTestReport {
    reports {
        html.enabled true
        html.destination file("${buildDir}/jacoco/html/")
        csv.enabled false
        xml.enabled true
        xml.destination file("${buildDir}/jacoco/test.xml")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
